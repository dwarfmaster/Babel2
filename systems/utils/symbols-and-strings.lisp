(in-package :utils)

;; ############################################################################

;; ----------------------------------------------------------------------------
;; configuration:

(defvar *default-id-base-name* "ID")
(defvar *default-var-id-base-name* "?VAR")
(defvar *default-const-id-base-name* "CONST")
(defvar *default-sticker-name* "STICKER")
;; ----------------------------------------------------------------------------
;; private system functionality:

(defparameter *nid-table* (make-hash-table :test #'equal))

(proclaim '(inline get-next-id-number))
(defun get-next-id-number (name)
  "Return the next number to create the next unique id with the given name."
  (declare (type string name))
  (if (gethash name *nid-table*)
      (incf (gethash name *nid-table*))
      (setf (gethash name *nid-table*) 1)))

(proclaim '(inline remove-numeric-tail))
(defun remove-numeric-tail (name)
  (declare (type string name))
  (loop for i from (- (length name) 1) downto 0
        for char = (elt name i)
        when (not (digit-char-p char))
        do (if (equal #\- char)
             (return (subseq name 0 i))
             (return name))
        finally (return name)))

(proclaim '(inline get-base-name))
(defun get-base-name (name &key
                      (remove-numeric-tail t)
                      (remove-question-mark t))
  "Return the base of the given name.
   - If base is a symbol then the base name is the symbol's name.
   - If base is a string then this string is the base name.
   - If remove-question-mark is true and the base name starts with a
     question-mark then this question-mark is removed from the base name.
   - If remove-numeric-tail is true and name is of the form 's-n',
     where s is a string of alphanumerical characters, and n is a string of
     numerical character, then the base is 's', i.e. the hyphen and trailing
     numerical characters are removed."
  (declare (type (or string symbol) name))
  (let* ((name (cond ((stringp name) (upcase name))
                    ((symbolp name) (symbol-name name))
                    (t (write-to-string name))))
        (name-as-string name))
    (if remove-numeric-tail (setq name (remove-numeric-tail name)))
    (if (string= name "") ;; for symbols like -5
      name-as-string
      (if (and remove-question-mark (char-equal #\? (elt name 0)))
        (subseq name 1)
        name))))

;; ----------------------------------------------------------------------------
;; public utilities:

(export '(get-base-name
          make-id
          make-var
          make-kw
          variable-p
          make-const
          reset-id-counters
          string-append
          mkstr
          symb
          internal-symb
          derive-base-name-from-type))

(unless (fboundp 'string-append)
  (defun string-append (&rest strings)
    "concatenates strings"
    (format nil "狺篝蜷铉螬┅ㄤ彐躅黼篝é蝈篝狎珲礤铘螬⒁弭躜铙篝蜷铉泔铘衢铋铉犰狎珲礤铘螽ㄦ矧磲铋狺狎珲礤铘螬ㄤ彐躅簌礅é蝈篝狎珲礤铘螬⑼犭骝弩璎躅轭翦蝾邃簌礅镬鏖翳簌礅镬钺礤黼篝狎珲礤铘螬磲脲簌礅镬ㄦ矧磲铋狺狎珲礤铘螬┅ㄤ彐躅轭翦蝾犰簌礅é蝈篝狎珲礤铘螬⑼犭犷轭翦蝾翳簌礅镬鏖翳簌礅镬钺礤黼篝狎珲礤铘螬ㄩ铘弪ㄦ矧磲铋狺狎珲礤铘螬┅ㄤ彐躅磲脲殇é镳糸镱犰钺礤⒚蝈狒犷蝈趱蝾躅轳蹂铛礅弪邃殇麒殂轶簌礅镬麒矬簌礅镬钺礤泔铙轶趔镦钺礤犷铛礅弪－扉箴黠螂ㄤ邈灬蝈豉疱矧簌礅镬篝蜷铉铛祆钺礤┅戾è忉箦钺礤ㄣ镱è铛祆钺礤溴驷蹯舡殇忉箦钺礤è簌礅镬钺礤簌礅镬钺礤钺礤┅è篝蜷铉钺礤钺礤┅┅磲脲簌礅镬ㄦ矧磲铋豪狺┉幄忉箦钺礤ㄧ弭铄舡殇铛礅弪忉箦钺礤┅┅ㄤ彐躅磲脲鲠é镳糸镱犰钺礤⒚蝈狒犷蝈趱蝾躅轳蹂泼鲠蜷徕戾簌礅镬物翦翳狒殒秕栳鲥翳汨镩沐忮赭邋疳篌轭篝蜷铉矧簌礅镬狍翳狎珲礤铘麸磲脲鲠颥磲脲泔铙矧磲脲殇翳孱疳篌轸翳篝蜷铉涉秕疳篌轸翳簌礅镬翳孱翳轫痨屙孱翎糸镱鏖祆箝眇禊翎脲翳簌礅镬钺礤骝镯轸骢螋桢殓铒蝈翳簌礅镬－扉箴黠螂ㄤ邈灬蝈豉疱矧铛祆篝蜷铉簌礅镬钺礤┅磲脲殇ㄩ钺礤ㄦ矧磲铋⒖幄ㄧ弭忉箦钺礤钺礤┅溴驷蹯舡鲠颦殇忉箦钺礤┅ㄤ彐躅磲脲膑钺礤Ⅳ犭弩篝蜷铉瀹绠堍翦篝堍犷趱蝾轸轭麸簌礅镬轭翦蝾邃轭翳脲黠蜾疳汶徵瀣瀹绠呼弩簪鲠祯弩ㄩ铘弪篝蜷铉躔汜箦钺礤弘妁黠蜾┅ㄤ彐躅鲠蜷徕戾⒃弩麒弭桢轶鲠蜷徕戾楫瀹麒弭桢轸轶簌礅镬镦麒殂翳钺礤篝狎趔鏖翳聃弩糸镱磲螂ㄡ钿簌礅镬ㄥ聃犰ㄣ栳簌礅镬钺礤癌＼咯┅ㄤ彐躅磲脲泔铙é镳糸镱犰钺礤蝈盹鲥铛礤蜷悱翎殪舂⒚蝈狒犷蝈趱蝾躅轳蹂泼泔铙翎铘簌礅镬－扉箴黠螂ㄤ邈灬蝈豉疱矧铛祆篝蜷铉簌礅镬钺礤┅磲脲殇ㄩ钺礤ㄧ弭忉箦钺礤钺礤候屙秭瀛铛礤蜷悱翎殪蝈盹鲥铛礤蜷悱翎殪溴驷蹯舡泔铙舡殇忉箦钺礤┅ㄤ彐躅蝈箦舡殇泔躅翦蝮ī⒁弩弭犰翳泔躅翦蝮骘翳铛礅弪邃殇螽箦翩铋洵翎忪濯磲脲栳箬翎忪呼弩＇羼踽飑舂ㄤ彐躅溴蜷鲥忉箦钺礤骝镯豉疱豉疱⑶轹孱豉疱轭箫礤骘蝽蝈趱蝾篝蜷铉忉箦钺礤ㄣ镱è铛祆豉疱Ⅷè簌礅镬豉疱簌礅镬钺礤豉疱┅è扉篝豉疱ㄤ弪轹瀛忉箦钺礤骝镯豉疱ㄣ狎豉疱┅ㄥ蝌矧⒄铄疱泗邃豉疱徜汜箦麸溴蜷鲥忉箦钺礤骝镯豉疱┅┅换痱镧换蝈箦舡殇泔躅翦蝮换痧蜷铘换祜镳骘鲠蜷徕戾轭换扉篝磲脲簌礅镬⒖骘铫磲脲簌礅镬⒖骘铫磲脲簌礅镬⒖骘锉换磲脲簌礅镬⒖骘锃忉颌磲脲簌礅镬⒖骘锃忉虮换磲脲殇Э忉磲脲殇Э忉换磲脲殇Э骘锃床磲脲殇Э骘锃床┅换泔祆邈ㄣ镱鲠蜷徕戾磲脲鲠鲠蜷徕戾┅┅换换蝈篚祠换换è：科舷：科舷暴ǎ嚎葡：科舷博ǎ嚎葡媳：科舷杯暴换ǎ嚎葡檄铝：科舷铝噎暴ǎ嚎葡檄铝冶：科舷铝冶暴换ǎ嚎铝诃：柯邻畅ǎ嚎铝诃：柯邻穿ǎ嚎葡檄床：科舷床畅换ǎ嚎葡檄床：科舷床穿换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换篝蜷铉豸殪轸殄蠛换ㄥ痫螋Ж滹黝汜箦躔汜箦篝蜷铉蝈痨徙蝈痨徙瀛汨狎蝈徜骈戾狍篝蜷铉蝈徜躅糸篝蜷铉殒蝈盹鲥瘐钽趱狒轱扉箴俱犴屐汜箦汜礤飙汜箦眷轶皓ㄤ彐躅瘐钽趱狒轱瞽ㄣ栳颟ㄦ轭汨狎弋缓唷浚ī苘堍┅ㄤ彐躅蝈盹鲥瘐钽趱狒轱篝蜷铉⒁屦灬沐瘐钽趱狒轱鏖翳箴徙弩轭篝蜷铉篚怏糸趱翦殒＼箴徙＇瘐钽趱狒轱瞽篝蜷铉┅ㄤ彐躅滹黝汜箦篝颟ㄦ矧磲铋狺篝颟ㄤ彐躅躔汜箦篝颟ㄦ矧磲铋豪狺篝颟ㄤ彐躅篝蜷铉殒簌礅镬矧篝蜷铉⑼犭篝蜷铉骝镯簌礅镬铛礅弪镦篝蜷铉箝黹灬麸扉篝殒ㄣ镱è篝蜷铉簌礅镬矧篝蜷铉簌礅镬矧篝蜷铉è铛礅弪簌礅镬矧篝蜷铉黼篝簌礅镬矧篝蜷铉┅簌礅镬钺礤簌礅镬矧篝蜷铉┅┅ㄤ彐躅篝蜷铉蝈痨徙篝虮篚獗篚獠⑽镱溴篝蝓泗轹屐蝈痨徙弩犰镢沲蝈钽弩镦篚獗轭篝虮怡篚獠戾è篝虮篝蜷铉篝虮┅篝虿篚獗篝蜷铉篚獗┅篚獠篝蜷铉篚獠┅ㄩ钿屮癌祜镳殒篝蜷铉羼踽篝虮篚獗后翎螋轭溴哄钿黹戾铉翳篝虮ǐ轭溴戾铉翳篚獗┅┅滹箦赳篝虿ㄣ镱汜翦钺翦篝蜷铉篝虿篚獠┅ㄩ钽轭溴戾铉翳篚獗┅屐箦滹箦赳篝虿ㄣ镱汜翦钺翦篝蜷铉篝虿篚怏羼篝虮轭溴ū轭溴暴┅ㄩ钽轭溴暴躅戾篌轭溴戾铉翳篝虮┅蝈趱蝾篝虿┅ㄤ彐躅蝈痨徙瀛汨狎篝蜷铉矧殓蝈痨徙屙孱舂⒛弩趄蹉糸鲥禊蝈痨徙弩犰镢沲蝌孱沐镦汨狎徙翦轭篝蜷铉怡犷雉桢镱澧ㄤ邈灬蝈豉疱篝蜷铉篝蜷铉豉疱矧汨狎徙翦篝蜷铉矧殓豉疱矧汨狎徙翦篝蜷铉蝈痨徙屙孱舂趄犷箧矧篝蜷铉狎珲礤铘麸汨狎徙翦蝮躅戾篌豉疱矧殓с栳蜥泗弪箦翩矧殓ㄣ栳蜥泗弪矧殓┅躅戾篌豉疱蝈痨徙屙孱с栳蜥泗弪箦翩蝈痨徙屙孱ㄣ栳蜥泗弪蝈痨徙屙孱舂┅磲忮翳弪濮犰箫麽麸滹翳轶鏖翳磲瓠扉脲泔铙趄蹉糸镱轭篝遽镦祜镳犷篝殪忮溴篝蝓泗轹蹇躞轭痫箝糸镱矧殓篝蜷铉后翎螋擤滹弩瞌黠螂忮汜躞镦怩轭沣祜镳骘骝镯麸ū戾铉翳篝蜷铉┅麒孱ㄥ矧殓ㄡ蝈篝蜷铉椹滹箦翩ㄡ蝈篝蜷铉椹蝈痨徙屙孱舂篝蜷铉ㄤ彐躅蝈徜骈戾狍篝蜷铉疳翳钺礤ㄤ邈灬蝈豉疱疳翳钺礤疳翳钺礤┅戾è矬磲脲篝蜷铉秕麴豸篝蝈犴┅鏖翳镳孱骈戾ㄩ痱镡瀛骈戾疳翳钺礤┅祜镳骘汨狎蝈徜汨狎轶铋飑麒殪汨狎滹黩轸瀛汨狎汨狎矬┅ㄧ弭秕麴豸篝蝈犴篝蜷铉矬┅ㄤ彐珏铄蜷蝈徜躅糸篝蜷铉躅糸飙疳螋脲篝狎骝镯孱犰祜鳝雉桢颦脲螬ê滹沲礤铘狒轱⒁弭躜铙疳螋镦翳篝蜷铉躅糸翳躅糸飙疳螋轶孱泔躅翦蝈洚┅ㄤ彐礤翳镤蝈徜躅糸è篝蜷铉篝蜷铉躅糸飙疳螋汨狎徙翦颟脲篝狎癌ㄦ蝻憝孱铋飑篚怏羼篝蜷铉篝狎痫箝糸镱躅糸飙疳螋篝蜷铉后翎螋篝狎烘蝻憝孱骝镯孱洎┅ㄤ彐礤翳镤蝈徜躅糸è篝蜷铉篝蜷铉躅糸飙疳螋篝蜷铉脲篝狎癌ㄦ蝻憝孱铋飑篚怏羼篝蜷铉篝狎箦狎汨躅糸飙疳螋篝蜷铉后翎螋篝狎烘蝻憝孱骝镯孱洎┅ㄤ彐躅汜礤飙汜箦眷轶ㄣ犴屐篝蜷铉⑸铙弪忮赭邋祜麇蜚狍犷躔疱蜚狍汨狎犷磲脲弼弪翳轭躔疱蜚狍澧ㄤ邈灬蝈篝蜷铉汜礤飙篝蜷铉┅戾è痱轭舡痱弭豉铋飑鏖翳秕麴豸麸篝蜷铉蝈篚祠祜镳鏖翳灬篝麽蟓祜麇蜚狍骘徙蝻篌汜礤飙篝蜷铉麒孱ㄡ钿灬篝麽蟓祜麇蜚狍躔疱颦汜箦悌滹痱轭蝈篚祠殒祜麇颦汜箦悌滹箦翩灬篝麽蟓祜麇蜚狍舂屐箦滹箦翩灬篝麽蟓祜麇蜚狍铋飑滹痱轭ㄣ栳颦躔汜箦悌蝈篚祠┅┅ㄤ彐躅扉箴俱犴屐汜箦篝蜷铉脲ㄦ蝻憝骈蝮舂⒁屙秭忮赭邋黠蜾犷磲脲犰黠蜾躔疱蜚狍瀣屮沐痿翳骈蝮舢阻孱骝镯骈蝮轶趄蹂翳骈蝮黠蜾轶犰箫躔疱蜚狍瀹ㄤ邈灬蝈篝蜷铉篝蜷铉┅戾è痱轭舡痱弭豉铋飑鏖翳秕麴豸麸篝蜷铉蝈篚祠祜镳鏖翳灬篝麽蟓溽箬骘徙蝻篌篝蜷铉骘忮祜戾铉翳篝蜷铉殒ㄥ耢＼滹箦翩灬篝麽蟓溽箬舂屐箦滹痱镧ㄣ镱è犷癌骝镯骈蝮舂痱轭ㄣ栳颦躔汜箦悌蝈篚祠┅灬篝麽蟓溽箬痱轭ㄣ栳颦躔汜箦悌蝈篚祠┅痱轭ㄣ栳颦滹黝汜箦悌蝈篚祠┅箦翩灬篝麽蟓溽箬铋飑┅┅换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄥ痫螋Ж磲脲蜥钿镯篝蜷铉箴扉骈蝮舡黠蜾灬篝黠蜾┅ㄤ彐躅磲脲蜥钿镯篝蜷铉é镳糸镱犰戾铉翳嘲┅⑶孱弪狒弩犷蝈趱蝾蜥钿镯篝蜷铉戾铉翳膛吻匀澡篝蜷铉鏖祆泔铙轶箫戾禊镦溴汩磲溟玳趔犷劣蒙戾趑弪螽鏖翳秕麴豸麸篝蜷铉螬ㄤ雉轫弩ㄩ戾铉翳黩轸瀛汨狎ㄥ汜箦蜥钿镯旦è暴ㄣ镤瀛汨狎ǐ．ㄣ栳颦泔溴＼岍蜥钿镯捕┅┅è畅ㄣ镤瀛汨狎ǐ．ㄣ栳颦泔溴＼俩蜥钿镯捕┅┅è穿ㄣ镤瀛汨狎ǐ．ㄣ栳颦泔溴＼癌蜥钿镯卑┅┅螬┅ㄤ彐躅箴扉篝蜷铉箦疳蜥麸颟⒂痨轸篝蜷铉翎腴铉翳篚怏趄轭珞忮赭邋翳箦疳蜥麸虍女绠箴扉п猬沅彐КЗ殄熹Жп猝с洄у妲箴扉舡箦聃孱沐后痨轸箦聃孱沐箦疳蜥麸篝蜷铉┅ㄤ彐轭瀛泔眇殪弪磲泸箴扉篝蜷铉箦疳蜥麸颟啜戾è篝蜷铉篝蜷铉箦疳蜥麸箦疳蜥麸颟箴扉舡箦聃孱沐后痨轸箦聃孱沐箦疳蜥麸篝蜷铉┅ㄤ彐躅骈蝮舡黠蜾篝蜷铉戾è轭溴黠蜾孱箦狎汨篝蜷铉┅篚怏羼篝蜷铉轭溴黠蜾孱洎┅ㄤ彐躅灬篝黠蜾篝蜷铉戾è轭溴黠蜾孱箦狎汨篝蜷铉烘蝻憝孱舂┅ㄩ轭溴黠蜾孱篚怏羼篝蜷铉ǐ轭溴黠蜾孱洎篝蜷铉┅换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄥ痫螋Ж棂痂孱辁濠ㄤ彐躅棂痂孱辁篝蜷铉鏖翳箴徙弩篝蜷铉蝈痨徙篝蜷铉鏖翳箴徙弩换ㄨ痂孱辁Ⅳ桢镢遽睥